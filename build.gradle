version = '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    test.java.srcDirs 'src/test/java'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

task testMulti(type:Test) {
    include 'tests/*'
    maxParallelForks = 4
    reports {
        junitXml.enabled = false
        html.enabled = true
    }
}

task testSingle(type: Test){
    maxParallelForks = 1
    include 'threadsafe/*'

}

testSingle.mustRunAfter(testMulti)

ext {
    supportedVariables = ["password"]
}

tasks.withType(Test) {
    useJUnit()
    dependsOn 'cleanTest'
    supportedVariables.each {
        if (project.hasProperty(it)) {
            systemProperties[it] = project.properties[it]
        }
    }
    testLogging.exceptionFormat = 'full'
    testLogging {
        events "passed", "failed", "standardOut"
        displayGranularity 1
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.6.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.9.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.9.1'
}


